#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Запрос = Новый Запрос;	
	Запрос.Текст = "";
	
	ПараметрыЗапроса = Новый Структура();
	
	СформироватьТекстЗапросаТовары(Запрос.Текст, ПараметрыЗапроса);
	
	//Контроль заполненности "Товары.Склад"
	СформироватьТекстЗапросаКонтрольПоляСклад(Запрос.Текст, ПараметрыЗапроса);
	
	Для Каждого ПараметрЗапроса Из ПараметрыЗапроса Цикл	
		Запрос.УстановитьПараметр(ПараметрЗапроса.Ключ, ПараметрЗапроса.Значение);
	КонецЦикла;	
	
	ПакетРезультатов = Запрос.ВыполнитьПакет();
	
	//Контроль заполненности "Товары.Склад"
	РезультатЗапросаКонтрольПоляСклад = ПакетРезультатов[1];
	ВыборкаКонтрольПоляСклад = РезультатЗапросаКонтрольПоляСклад.Выбрать();
	
	Пока ВыборкаКонтрольПоляСклад.Следующий() Цикл
		СообщитьОбОшибкеКонтрольПоляСклад(ВыборкаКонтрольПоляСклад, Отказ);
	КонецЦикла;	 
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, ПолучитьПоляПрограммнойПроверкиЗаполнения());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьПоляПрограммнойПроверкиЗаполнения() 
	
	СписокПолей = Новый Массив;
	СписокПолей.Добавить("Товары.Склад");
	
	Возврат СписокПолей;
	
КонецФункции

Процедура СформироватьТекстЗапросаТовары(ТекстЗапроса, ПараметрыЗапроса)
	
	ТекстЗапроса = ТекстЗапроса + 
	"ВЫБРАТЬ
	|	Товары.НомерСтроки,
	|	Товары.Номенклатура,
	|	Товары.Склад,
	|	Товары.Количество
	|ПОМЕСТИТЬ ВТ_Товары
	|ИЗ
	|	&Товары КАК Товары";
	
	ПараметрыЗапроса.Вставить("Товары", Товары.Выгрузить(,"НомерСтроки, Номенклатура, Склад, Количество"));
	
КонецПроцедуры

Процедура СформироватьТекстЗапросаКонтрольПоляСклад(ТекстЗапроса, ПараметрыЗапроса)
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительЗапросовВПакете() +
	"ВЫБРАТЬ
	|	ВТ_Товары.НомерСтроки
	|ИЗ ВТ_Товары КАК ВТ_Товары
	|
	|ГДЕ ВТ_Товары.Номенклатура.Тип = Значение(Перечисление.ТипыНоменклатуры.Товар)
	|И ВТ_Товары.Склад = Значение(Справочник.Склады.ПустаяСсылка)";
	
КонецПроцедуры

Процедура СообщитьОбОшибкеКонтрольПоляСклад(ВыборкаКонтрольПоляСклад, Отказ)
	
	ТекстОшибки = СтрШаблон(Нстр("ru = 'Не заполнено поле ''%1'' в строке №%2'"), "Склад", ВыборкаКонтрольПоляСклад.НомерСтроки);
	ПутьКДанным = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", ВыборкаКонтрольПоляСклад.НомерСтроки, "Склад");
	ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, , ПутьКДанным, Отказ);
	
КонецПроцедуры	
	
#КонецОбласти	

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли