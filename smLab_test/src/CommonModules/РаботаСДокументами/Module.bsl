
#Область ПрограммныйИнтерфейс

// Получить структуру таблиц проведения.
// 
// Параметры:
//  ДокументОбъект - ДокументОбъект
// 
// Возвращаемое значение:
//  Структура - Получить структуру таблиц проведения:
// * ПартииТоваров  - ТаблицаЗначений
// * КонтрольПартииТоваров  - ТаблицаЗначений 
// * Продажи - ТаблицаЗначений
Функция ПолучитьСтруктуруТаблицПроведения(ДокументОбъект) Экспорт  
	
	СтруктураТаблиц = Новый Структура; 
	
	Если ДокументОбъект.Движения.Найти("ПартииТоваров") Тогда		
		СтруктураТаблиц.Вставить("ПартииТоваров", ДокументОбъект.Движения.ПартииТоваров.Выгрузить());
		СтруктураТаблиц.Вставить("КонтрольПартииТоваров", ДокументОбъект.Движения.ПартииТоваров.Выгрузить(,"Организация,Склад,Номенклатура,ПартияНоменклатуры"));		
	КонецЕсли;	
	
	Если ДокументОбъект.Движения.Найти("Продажи") Тогда		
		СтруктураТаблиц.Вставить("Продажи", ДокументОбъект.Движения.Продажи.Выгрузить());		
	КонецЕсли;
	 
	Возврат СтруктураТаблиц;
	
КонецФункции

// Выполнить стандартный контроль.
// 
// Параметры:
//  СтруктураТаблиц - ТаблицаЗначений
// 
// Возвращаемое значение:
//  Структура - Выполнить стандартный контроль:
// * Ошибка - Булево - 
// * ТекстОшибки - Строка - 
Функция ВыполнитьСтандартныйКонтроль(СтруктураТаблиц) Экспорт
	
		РезультатКонтроля = Новый Структура("Ошибка, ТекстОшибки", Ложь, "");
	
		Если СтруктураТаблиц.Свойство("ПартииТоваров") И СтруктураТаблиц.ПартииТоваров.Количество() Тогда
			КонтрольОстатков(СтруктураТаблиц.ПартииТоваров, РезультатКонтроля);
		КонецЕсли;
		
		Возврат РезультатКонтроля;
	
КонецФункции	

// Установить стандартные блокировки.
// 
// Параметры:
//  БлокировкаДанных - БлокировкаДанных
//  СтруктураТаблиц - ТаблицаЗначений
// 
Процедура УстановитьСтандартныеБлокировки(БлокировкаДанных, СтруктураТаблиц) Экспорт
	
	Если СтруктураТаблиц.Свойство("ПартииТоваров") И СтруктураТаблиц.ПартииТоваров.Количество() Тогда
		НоваяБлокировка = БлокировкаДанных.Добавить("РегистрНакопления.ПартииТоваров");
		НоваяБлокировка.Режим          = РежимБлокировкиДанных.Исключительный;
		НоваяБлокировка.ИсточникДанных = СтруктураТаблиц.ПартииТоваров;
		НоваяБлокировка.ИспользоватьИзИсточникаДанных("Организация", "Организация");
		НоваяБлокировка.ИспользоватьИзИсточникаДанных("Склад", "Склад");
		НоваяБлокировка.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
		НоваяБлокировка.ИспользоватьИзИсточникаДанных("ПартияНоменклатуры", "ПартияНоменклатуры");
	КонецЕсли;
	
КонецПроцедуры	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура КонтрольОстатков(ТаблицаКонтроля, РезультатКонтроля)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ТаблицаКонтроля.Организация,
	|	ТаблицаКонтроля.Склад,
	|	ТаблицаКонтроля.Номенклатура,
	|	ТаблицаКонтроля.ПартияНоменклатуры
	|ПОМЕСТИТЬ втКонтроля
	|ИЗ
	|	&ТаблицаКонтроля КАК ТаблицаКонтроля
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПартииТоваровОстатки.Организация,
	|	ПартииТоваровОстатки.Склад,
	|	ПартииТоваровОстатки.Номенклатура,
	|	ПартииТоваровОстатки.ПартияНоменклатуры,
	|	ПартииТоваровОстатки.КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.ПартииТоваров.Остатки КАК ПартииТоваровОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКонтроля КАК втКонтроля
	|		ПО ПартииТоваровОстатки.Организация = втКонтроля.Организация
	|		И ПартииТоваровОстатки.Склад = втКонтроля.Склад
	|		И ПартииТоваровОстатки.Номенклатура = втКонтроля.Номенклатура
	|		И ПартииТоваровОстатки.ПартияНоменклатуры = втКонтроля.ПартияНоменклатуры
	|ГДЕ
	|	ПартииТоваровОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("ТаблицаКонтроля", ТаблицаКонтроля);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		 РезультатКонтроля.Ошибка = Истина;
		 
		 Если ПустаяСтрока(РезультатКонтроля.ТекстОшибки) Тогда
		 	ТекстСообщения = НСтр("ru = 'Номенклатура %1(Партия: %2): недостаточно свободного остатка по организации %3 на %4. (кол. :%5)'");
		 Иначе
		 	ТекстСообщения = Символы.ПС + НСтр("ru = 'Номенклатура %2(Партия: %1): недостаточно свободного остатка по организации %3 на %4. (кол. :%5)'");	
		 КонецЕсли;	
		 	
		 ТекстСообщения = СтрШаблон(ТекстСообщения, Строка(Выборка.Номенклатура),
		 	Строка(Выборка.ПартияНоменклатуры),
		 	Строка(Выборка.Организация),
		 	Строка(Выборка.Склад),
		 	Строка(Выборка.КоличествоОстаток));
		 	
		 РезультатКонтроля.ТекстОшибки = РезультатКонтроля.ТекстОшибки + ТекстСообщения;	
		 	
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти	
